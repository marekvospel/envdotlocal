key = {
  (ASCII_ALPHA | "_")+
  ~ (ASCII_ALPHANUMERIC | "_")*
}
till_end = _{
  !(EOI | NEWLINE)
  ~ ANY
}

no_quote = _{
  !("'" | "\"" | "\"\"\"" | NEWLINE)
  ~ ANY
}
unquoted_string = ${ unquoted_inner }
unquoted_inner = @{ no_quote ~ unquoted_char* }
unquoted_char = {
    !(WHITE_SPACE | "\\") ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

single_string = _{ "'" ~ single_inner ~ "'" }
single_inner = @{ single_char* }
single_char = {
    !("'" | NEWLINE | "\\") ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

double_string = _{ "\"" ~ double_inner ~ "\"" }
double_inner = @{ double_char* }
double_char = {
    !("\"" | NEWLINE | "\\") ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

triple_string = _{ "\"\"\"" ~ triple_inner ~ "\"\"\"" }
triple_inner = @{ triple_char* }
triple_char = {
    !("\"\"\"" | "\\") ~ ANY
    | "\\" ~ ("\"" | "'" | "\\" | "/" | "b" | "f" | "n" | "r" | "t")
    | "\\" ~ ("u" ~ ASCII_HEX_DIGIT{4})
}

value = { triple_string | double_string | single_string | unquoted_string }
line = { (NEWLINE | SOI) ~ (WHITE_SPACE* ~ ("export" ~ WHITE_SPACE+)?~ key ~ "=" ~ value?)? ~ till_end* }

file = {
  line*
}
